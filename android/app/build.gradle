plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    // id "com.google.gms.google-services"
}

// Local Properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    throw new GradleException("versionCode not found in local.properties")
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    throw new GradleException("versionName not found in local.properties")
}

// SDK Properties
def sdkProperties = new Properties()
def sdkPropertiesFile = rootProject.file('sdk.properties')
if (sdkPropertiesFile.exists()) {
    sdkPropertiesFile.withReader('UTF-8') { reader ->
        sdkProperties.load(reader)
    }
}

def flutterMinSdkVersion = sdkProperties.getProperty('flutter.minSdkVersion')
if (flutterMinSdkVersion == null) {
    throw new GradleException("minSdkVersion not found in sdk.properties")
}

def flutterCompileSdkVersion = sdkProperties.getProperty('flutter.compileSdkVersion')
if (flutterCompileSdkVersion == null) {
    throw new GradleException("compileSdkVersion not found in sdk.properties")
}

def flutterTargetSdkVersion = sdkProperties.getProperty('flutter.targetSdkVersion')
if (flutterTargetSdkVersion == null) {
    throw new GradleException("targetSdkVersion not found in sdk.properties")
}

// Keystore Properties
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.tcg.fruitcraft.trading.card.game.battle"
    compileSdkVersion flutterCompileSdkVersion.toInteger()
    ndkVersion "25.2.9519653"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.tcg.fruitcraft.trading.card.game.battle"
        minSdkVersion flutterMinSdkVersion
        targetSdkVersion flutterTargetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
       release {
           keyAlias keystoreProperties['keyAlias']
           keyPassword keystoreProperties['keyPassword']
           storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
           storePassword keystoreProperties['storePassword']
       }
    }
    
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release 
        }
        debug
        {
            signingConfig signingConfigs.release 
        }
    }
}

flutter {
    source '../..'
}

dependencies {}
